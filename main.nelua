require 'config'
require 'raylib'
require 'allocators.gc'
## if PLATFORM_WEB then
   gc:pause() -- conservative GCs cannot run automatically with emscripten
## end
require 'vector'
require 'string'
require 'rotor'
require 'game.resources'

local BACKGROUND_COLOR: Color <const> = { 0x22, 0x20, 0x34, 255 }

local TXT_TEXT_PT_BR: string <const> = "Após tanto esforço, a princesa\nfinalmente poderá escapar desta \nterrível prisão!"
local TXT_TEXT_EN_US: string <const> = "After so much effort, the princess\ncan finally escape this \nterrible prison!"
local txt_text: string = #[DEFAULT_LANG]# == 'en-US' and TXT_TEXT_EN_US or TXT_TEXT_PT_BR

-- [ Main game records  and enums[
global EntityTag = @enum {
   Player = 1,
   TileMap = 2,
   Wall = 4,
   GearSlot = 8,
   Box = 16,
   Gear = 32,
   ClosedBridge = 64,
   OpenBridge = 128,
}

global CollisionState = @enum {
   None = 0,
   Enter,
   Stay
}

global PlayerInput = @record{
   direction: Vector2,
   action: boolean
}

global Globals: record{
   dt: float32,
   player_input: PlayerInput,
   engaged_slots: vector(boolean),
   camera: Camera2D,
   game_window: record{
      width: integer,
      height: integer,
      title: string
   },
   untracked_ids: vector(GenerationalIndex),
} = {
   dt = 0,
   player_input = {
      direction = { 0, 0 },
      action = false,
   },
   engaged_slots = { false, false, false },
   camera = {
      offset = { #[WIN_WIDTH]# / 2, #[WIN_HEIGHT]# / 2 },
      target = { #[PL_POS_X]#, #[PL_POS_Y]# },
      rotation = 0,
      zoom = 1
   },
   game_window = {
      #[WIN_WIDTH]# // 1,
      #[WIN_HEIGHT]# // 1,
      #[WIN_TITLE]#
   },
   untracked_ids = {},
}

require 'game'

global Game_Systems: record{
   colliders_sync: CollidersSyncSystem,
   collision_detection: CollisionDetectionSystem,

   player_controller: PlayerControllerSystem,
   box_controller: BoxControllerSystem,
   gear_controller: GearControllerSystem,

   engage_system: EngageSystem,
   bridge_system: BridgeSystem,

   obstacles_system: ObstaclesSystem,
   velocity_system: VelocitySystem,

   animation: AnimationSystem,

   sprite_painter: SpritePainter,

   show_colliders: ShowCollidersSystem,
   show_collisions: ShowCollisionsSystem,
   show_positions: ShowPositionsSystem,
} = {
   colliders_sync = CollidersSyncSystem.new(),
   collision_detection = CollisionDetectionSystem.new(),

   player_controller = PlayerControllerSystem.new(),
   box_controller = BoxControllerSystem.new(),
   gear_controller = GearControllerSystem.new(),

   engage_system = EngageSystem.new(),
   bridge_system = BridgeSystem.new(),

   obstacles_system = ObstaclesSystem.new(),
   velocity_system = VelocitySystem.new(),

   animation = AnimationSystem.new(),

   sprite_painter = SpritePainter.new(),

   show_colliders = ShowCollidersSystem.new(),
   show_collisions = ShowCollisionsSystem.new(),
   show_positions = ShowPositionsSystem.new(),
}

require 'game.tilemap_creator'
require 'game.levels'
-- ] Main game records ]

local function update_player_input_values()
   local right_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_D) or Raylib.IsKeyDown(KeyboardKey.KEY_RIGHT) or Raylib.IsKeyDown(KeyboardKey.KEY_L)
   local up_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_W) or Raylib.IsKeyDown(KeyboardKey.KEY_UP) or Raylib.IsKeyDown(KeyboardKey.KEY_I)
   local left_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_A) or Raylib.IsKeyDown(KeyboardKey.KEY_LEFT) or Raylib.IsKeyDown(KeyboardKey.KEY_J)
   local down_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_S) or Raylib.IsKeyDown(KeyboardKey.KEY_DOWN) or Raylib.IsKeyDown(KeyboardKey.KEY_K)

   local action_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_SPACE) or Raylib.IsKeyDown(KeyboardKey.KEY_ENTER)

   Globals.player_input.direction.x = left_is_down and -1 or (right_is_down and 1 or 0)
   Globals.player_input.direction.y = up_is_down and -1 or (down_is_down and 1 or 0)
   Globals.player_input.action = action_is_down
end

-- [ Game initialization [
## if not PLATFORM_WEB then
   Raylib.SetTargetFPS(60)
## end

Raylib.InitWindow(Globals.game_window.width, Globals.game_window.height, Globals.game_window.title)
resources:load()

local function update_system(system: System*)
   system.system_data:update(storages.entity, Globals.untracked_ids)
end

local function update_all_systems()
   update_system(Game_Systems.colliders_sync.system)
   update_system(Game_Systems.collision_detection.system)

   update_system(Game_Systems.box_controller.system)
   update_system(Game_Systems.gear_controller.system)
   update_system(Game_Systems.player_controller.system)

   update_system(Game_Systems.engage_system.system)
   update_system(Game_Systems.bridge_system.system)

   update_system(Game_Systems.obstacles_system.system)
   update_system(Game_Systems.velocity_system.system)

   update_system(Game_Systems.animation.system)

   update_system(Game_Systems.sprite_painter.system)

   update_system(Game_Systems.show_colliders.system)
   update_system(Game_Systems.show_collisions.system)
   update_system(Game_Systems.show_positions.system)
end

local player_entity = PlayerEntity.create({ #[PL_POS_X]#, #[PL_POS_Y]# }, Globals.untracked_ids)
create_level({ 32*8, 32*3 })

update_all_systems()

local player_position_opt = storages.position:get_entry(player_entity.position_id)
global player_position: Position* = player_position_opt:get()

local player_animations_opt = storages.animations:get_entry(player_entity.animations_id)
local player_animations: Animations* = player_animations_opt:get()
player_animations.current_animation_index = 1

-- ] Game initialization ]

-- [ Game loop [
local function update_draw_frame() -- 15 90
   local game_finalized = player_position.position.y < 400

   -- [ Update [
   Globals.dt = Raylib.GetFrameTime()

   update_player_input_values()

   Game_Systems.colliders_sync:run()
   Game_Systems.collision_detection:run()

   Game_Systems.player_controller:run()
   Game_Systems.box_controller:run()
   Game_Systems.gear_controller:run()

   Game_Systems.engage_system:run()
   Game_Systems.bridge_system:run()

   Game_Systems.obstacles_system:run()
   Game_Systems.velocity_system:run()

   Game_Systems.animation:run()

   Globals.camera.target = player_position.position

   if Raylib.IsKeyPressed(KeyboardKey.KEY_T) then
      txt_text = txt_text == TXT_TEXT_EN_US and TXT_TEXT_PT_BR or TXT_TEXT_EN_US
   end

   -- ] Update ]

   -- [ Draw [
   Raylib.BeginDrawing()
      Raylib.ClearBackground(BACKGROUND_COLOR)

      Raylib.BeginMode2D(Globals.camera)
         Raylib.DrawText(txt_text, 32*26, 32*22, 20, RAYWHITE)

         Game_Systems.sprite_painter:run(storages.sprite, storages.position)

         ## if SHOW_DEV_INFO then
            Game_Systems.show_colliders:run()
            Game_Systems.show_collisions:run()
            Game_Systems.show_positions:run()
         ## end

      Raylib.EndMode2D()
   Raylib.EndDrawing()
   -- ] Draw ]

   ## if PLATFORM_WEB then
      gc:run() -- safe to collect garbage here
   ## end
end

## if PLATFORM_WEB then
   emscripten_set_main_loop(update_draw_frame, 0, 1)
## else
   while not Raylib.WindowShouldClose() do
      update_draw_frame()
   end
##end

-- ] Game loop ]

-- [ Game De-initialization [
resources:unload()
Raylib.CloseWindow()
-- ] Game De-initialization ]
