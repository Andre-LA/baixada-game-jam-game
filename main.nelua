require 'raylib'
require 'vector'
require 'string'
require 'rotor'
require 'config'

-- [ Main game records  and enums[
global EntityTag = @enum {
   Player = 1,
   TileMap = 2,
   Wall = 4,
   GearSlot = 8,
   Box = 16,
   Gear = 32,
   ClosedBridge = 64,
   OpenBridge = 128,
}

global CollisionState = @enum {
   None = 0,
   Enter,
   Stay
}

global PlayerInput = @record{
   direction: Vector2,
   action: boolean
}

require 'game'

global Globals: record{
   player_input: PlayerInput,
   engaged_slots: vector(boolean),
   camera: record{
      position: Vector2,
      camera: Camera2D
   },
   game_window: record{
      width: integer,
      height: integer,
      title: string
   },
   game_systems: record{
      colliders_sync: CollidersSync,
      collision_detection: CollisionDetection,

      player_controller: PlayerController,
      box_controller: BoxController,
      gear_controller: GearController,

      engage_system: EngageSystem,
      bridge_system: BridgeSystem,

      obstacles_system: ObstaclesSystem,
      velocity_system: VelocitySystem,

      animation: Animation,

      sprite_painter: SpritePainter,

      show_colliders: ShowColliders,
      show_collisions: ShowCollisions,
      show_positions: ShowPositions,
   },
   untracked_ids: vector(GenerationalIndex),
} = {
   player_input = {
      direction = { 0, 0 },
      action = false,
   },
   engaged_slots = { false, false },
   camera = {
      position = { #[PL_POS_X]#, #[PL_POS_Y]# },
      camera = {
         offset = { #[WIN_WIDTH]# / 2, #[WIN_HEIGHT]# / 2 },
         target = { #[PL_POS_X]#, #[PL_POS_Y]# },
         rotation = 0,
         zoom = 1
      }
   },
   game_window = {
      #[WIN_WIDTH]# // 1,
      #[WIN_HEIGHT]# // 1,
      #[WIN_TITLE]#
   },
   untracked_ids = {},
   game_systems = {
      colliders_sync = CollidersSync.new(),
      collision_detection = CollisionDetection.new(),

      player_controller = PlayerController.new(),
      box_controller = BoxController.new(),
      gear_controller = GearController.new(),

      engage_system = EngageSystem.new(),
      bridge_system = BridgeSystem.new(),

      obstacles_system = ObstaclesSystem.new(),
      velocity_system = VelocitySystem.new(),

      animation = Animation.new(),

      sprite_painter = SpritePainter.new(),

      show_colliders = ShowColliders.new(),
      show_collisions = ShowCollisions.new(),
      show_positions = ShowPositions.new(),
   },
}
-- ] Main game records ]

local function update_player_input_values()
   local right_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_D) or Raylib.IsKeyDown(KeyboardKey.KEY_RIGHT) or Raylib.IsKeyDown(KeyboardKey.KEY_L)
   local up_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_W) or Raylib.IsKeyDown(KeyboardKey.KEY_UP) or Raylib.IsKeyDown(KeyboardKey.KEY_I)
   local left_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_A) or Raylib.IsKeyDown(KeyboardKey.KEY_LEFT) or Raylib.IsKeyDown(KeyboardKey.KEY_J)
   local down_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_S) or Raylib.IsKeyDown(KeyboardKey.KEY_DOWN) or Raylib.IsKeyDown(KeyboardKey.KEY_K)

   local action_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_SPACE) or Raylib.IsKeyDown(KeyboardKey.KEY_ENTER)

   Globals.player_input.direction.x = left_is_down and -1 or (right_is_down and 1 or 0)
   Globals.player_input.direction.y = up_is_down and -1 or (down_is_down and 1 or 0)
   Globals.player_input.action = action_is_down
end

-- [ Game initialization [
Raylib.InitWindow(Globals.game_window.width, Globals.game_window.height, Globals.game_window.title)
Raylib.SetTargetFPS(60)

local function update_system(system: System*)
   system.system_data:update(storages.entity, Globals.untracked_ids)
end

local function update_all_systems()
   update_system(Globals.game_systems.colliders_sync.system)
   update_system(Globals.game_systems.collision_detection.system)

   update_system(Globals.game_systems.player_controller.system)
   update_system(Globals.game_systems.box_controller.system)
   update_system(Globals.game_systems.gear_controller.system)

   update_system(Globals.game_systems.engage_system.system)
   update_system(Globals.game_systems.bridge_system.system)

   update_system(Globals.game_systems.obstacles_system.system)
   update_system(Globals.game_systems.velocity_system.system)

   update_system(Globals.game_systems.animation.system)

   update_system(Globals.game_systems.sprite_painter.system)

   update_system(Globals.game_systems.show_colliders.system)
   update_system(Globals.game_systems.show_collisions.system)
   update_system(Globals.game_systems.show_positions.system)
end

local player_entity = PlayerEntity.create({ #[PL_POS_X]#, #[PL_POS_Y]# }, 1, 1, Globals.untracked_ids)
local wall_entity = WallEntity.create({ 60, 60 }, 1, Globals.untracked_ids)
local box_entity = BoxEntity.create({ 120, 120 }, 1, Globals.untracked_ids)

local gear_entity_1 = GearEntity.create({ 120, 60 }, Globals.untracked_ids)
local gear_slot_entity_1 = GearSlotEntity.create({ 220, 120 }, 0, Globals.untracked_ids)
local bridge_entity_1 = BridgeEntity.create({ 60, 120 }, 0, Globals.untracked_ids)

update_all_systems()

local player_position_opt = storages.position:get_entry(player_entity.position_id)
local player_position: Position* = player_position_opt:get()

local player_animations_opt = storages.animations:get_entry(player_entity.animations_id)
local player_animations: Animations* = player_animations_opt:get()
player_animations.current_animation_index = 1

-- ] Game initialization ]

-- [ Game loop [
while not Raylib.WindowShouldClose() do
   -- [ Update [
   local dt = Raylib.GetFrameTime()

   update_player_input_values()

   Globals.game_systems.colliders_sync:run(storages.position, storages.velocity, storages.collider)
   Globals.game_systems.collision_detection:run(storages.collider, storages.collisions)

   Globals.game_systems.player_controller:run(storages.player, storages.velocity, Globals.player_input)
   Globals.game_systems.box_controller:run(storages.box, storages.position, storages.collisions, storages.velocity, Globals.player_input, player_position.position)
   Globals.game_systems.gear_controller:run(storages.gear, storages.collisions, storages.velocity, storages.position, storages.animations, Globals.player_input, Globals.engaged_slots)

   Globals.game_systems.engage_system:run(storages.collisions, storages.gear_slot, Globals.engaged_slots)
   Globals.game_systems.bridge_system:run(storages.bridge, storages.collider, Globals.engaged_slots)

   Globals.game_systems.obstacles_system:run(storages.obstacles, storages.collisions, storages.velocity)
   Globals.game_systems.velocity_system:run(storages.velocity, storages.position)

   Globals.game_systems.animation:run(storages.animations, storages.sprite, dt)
   -- ] Update ]

   -- [ Draw [
   Raylib.BeginDrawing()
      Raylib.ClearBackground(RAYWHITE)

      Globals.game_systems.sprite_painter:run(storages.sprite, storages.position)

      Globals.game_systems.show_colliders:run(storages.collider)
      Globals.game_systems.show_collisions:run(storages.collisions)
      Globals.game_systems.show_positions:run(storages.position)
   Raylib.EndDrawing()
   -- ] Draw ]
end
-- ] Game loop ]

-- [ Game De-initialization [
Raylib.CloseWindow()
-- ] Game De-initialization ]
