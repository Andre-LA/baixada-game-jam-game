require 'raylib'
require 'allocators.gc'
require 'vector'
require 'string'
require 'rotor'
require 'config'

-- [ Main game records  and enums[
global EntityTag = @enum {
   Player = 1,
   TileMap = 2,
   Wall = 4,
   GearSlot = 8,
   Box = 16,
   Gear = 32,
   ClosedBridge = 64,
   OpenBridge = 128,
}

global CollisionState = @enum {
   None = 0,
   Enter,
   Stay
}

global PlayerInput = @record{
   direction: Vector2,
   action: boolean
}

require 'game'

global Globals: record{
   player_input: PlayerInput,
   engaged_slots: vector(boolean),
   camera: Camera2D,
   game_window: record{
      width: integer,
      height: integer,
      title: string
   },
   game_systems: record{
      colliders_sync: CollidersSync,
      collision_detection: CollisionDetection,

      player_controller: PlayerController,
      box_controller: BoxController,
      gear_controller: GearController,

      engage_system: EngageSystem,
      bridge_system: BridgeSystem,

      obstacles_system: ObstaclesSystem,
      velocity_system: VelocitySystem,

      animation: Animation,

      sprite_painter: SpritePainter,

      show_colliders: ShowColliders,
      show_collisions: ShowCollisions,
      show_positions: ShowPositions,
   },
   untracked_ids: vector(GenerationalIndex),
} = {
   player_input = {
      direction = { 0, 0 },
      action = false,
   },
   engaged_slots = { false, false, false },
   camera = {
      offset = { #[WIN_WIDTH]# / 2, #[WIN_HEIGHT]# / 2 },
      target = { #[PL_POS_X]#, #[PL_POS_Y]# },
      rotation = 0,
      zoom = 1
   },
   game_window = {
      #[WIN_WIDTH]# // 1,
      #[WIN_HEIGHT]# // 1,
      #[WIN_TITLE]#
   },
   untracked_ids = {},
   game_systems = {
      colliders_sync = CollidersSync.new(),
      collision_detection = CollisionDetection.new(),

      player_controller = PlayerController.new(),
      box_controller = BoxController.new(),
      gear_controller = GearController.new(),

      engage_system = EngageSystem.new(),
      bridge_system = BridgeSystem.new(),

      obstacles_system = ObstaclesSystem.new(),
      velocity_system = VelocitySystem.new(),

      animation = Animation.new(),

      sprite_painter = SpritePainter.new(),

      show_colliders = ShowColliders.new(),
      show_collisions = ShowCollisions.new(),
      show_positions = ShowPositions.new(),
   },
}

require 'game.tilemap_creator'
require 'game.levels'
-- ] Main game records ]

local function update_player_input_values()
   local right_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_D) or Raylib.IsKeyDown(KeyboardKey.KEY_RIGHT) or Raylib.IsKeyDown(KeyboardKey.KEY_L)
   local up_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_W) or Raylib.IsKeyDown(KeyboardKey.KEY_UP) or Raylib.IsKeyDown(KeyboardKey.KEY_I)
   local left_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_A) or Raylib.IsKeyDown(KeyboardKey.KEY_LEFT) or Raylib.IsKeyDown(KeyboardKey.KEY_J)
   local down_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_S) or Raylib.IsKeyDown(KeyboardKey.KEY_DOWN) or Raylib.IsKeyDown(KeyboardKey.KEY_K)

   local action_is_down = Raylib.IsKeyDown(KeyboardKey.KEY_SPACE) or Raylib.IsKeyDown(KeyboardKey.KEY_ENTER)

   Globals.player_input.direction.x = left_is_down and -1 or (right_is_down and 1 or 0)
   Globals.player_input.direction.y = up_is_down and -1 or (down_is_down and 1 or 0)
   Globals.player_input.action = action_is_down
end

-- [ Game initialization [
## if PLATFORM_WEB then
   gc:pause() -- conservative GCs cannot run automatically with emscripten
## else
   Raylib.SetTargetFPS(60)
## end

Raylib.InitWindow(Globals.game_window.width, Globals.game_window.height, Globals.game_window.title)

local function update_system(system: System*)
   system.system_data:update(storages.entity, Globals.untracked_ids)
end

local function update_all_systems()
   update_system(Globals.game_systems.colliders_sync.system)
   update_system(Globals.game_systems.collision_detection.system)

   update_system(Globals.game_systems.box_controller.system)
   update_system(Globals.game_systems.gear_controller.system)
   update_system(Globals.game_systems.player_controller.system)

   update_system(Globals.game_systems.engage_system.system)
   update_system(Globals.game_systems.bridge_system.system)

   update_system(Globals.game_systems.obstacles_system.system)
   update_system(Globals.game_systems.velocity_system.system)

   update_system(Globals.game_systems.animation.system)

   update_system(Globals.game_systems.sprite_painter.system)

   update_system(Globals.game_systems.show_colliders.system)
   update_system(Globals.game_systems.show_collisions.system)
   update_system(Globals.game_systems.show_positions.system)
end

local player_entity = PlayerEntity.create({ #[PL_POS_X]#, #[PL_POS_Y]# }, Globals.untracked_ids)
create_level({ 32*8, 32*3 })

update_all_systems()

local player_position_opt = storages.position:get_entry(player_entity.position_id)
local player_position: Position* = player_position_opt:get()

local player_animations_opt = storages.animations:get_entry(player_entity.animations_id)
local player_animations: Animations* = player_animations_opt:get()
player_animations.current_animation_index = 1

-- ] Game initialization ]

-- [ Game loop [
local function update_draw_frame()
   -- [ Update [
   local dt = Raylib.GetFrameTime()

   update_player_input_values()

   Globals.game_systems.colliders_sync:run(storages.position, storages.velocity, storages.collider)
   Globals.game_systems.collision_detection:run(storages.collider, storages.collisions)

   Globals.game_systems.player_controller:run(storages.player, --[[storages.position, ]] storages.velocity, Globals.player_input)
   Globals.game_systems.box_controller:run(storages.box, storages.position, storages.collisions, storages.velocity, Globals.player_input, player_position.position)
   Globals.game_systems.gear_controller:run(storages.gear, storages.collisions, storages.velocity, storages.position, storages.animations, Globals.player_input, Globals.engaged_slots)

   Globals.game_systems.engage_system:run(storages.collisions, storages.gear_slot, Globals.engaged_slots)
   Globals.game_systems.bridge_system:run(storages.bridge, storages.collider, storages.sprite, Globals.engaged_slots)

   Globals.game_systems.obstacles_system:run(storages.obstacles, storages.collisions, storages.velocity)
   Globals.game_systems.velocity_system:run(storages.velocity, storages.position)

   Globals.game_systems.animation:run(storages.animations, storages.sprite, dt)

   Globals.camera.target = player_position.position

   if Raylib.IsKeyDown(KeyboardKey.KEY_N) then
      Globals.camera.zoom = Globals.camera.zoom + 0.05
   elseif Raylib.IsKeyDown(KeyboardKey.KEY_M) then
      Globals.camera.zoom = Globals.camera.zoom - 0.05
   end

   -- ] Update ]

   -- [ Draw [
   Raylib.BeginDrawing()
      Raylib.ClearBackground(RAYWHITE)

      Raylib.BeginMode2D(Globals.camera)
         Globals.game_systems.sprite_painter:run(storages.sprite, storages.position)

         Globals.game_systems.show_colliders:run(storages.collider)
         Globals.game_systems.show_collisions:run(storages.collisions)
         Globals.game_systems.show_positions:run(storages.position)


         --for i = -32, 32 do
            --for j = -32, 32 do
               --Raylib.DrawRectangleLines(32*i, 32*j, 32, 32, BLACK)
            --end
         --end

      Raylib.EndMode2D()
   Raylib.EndDrawing()
   -- ] Draw ]

   ## if PLATFORM_WEB then
      gc:run() -- safe to collect garbage here
   ## end
end

## if PLATFORM_WEB then
   emscripten_set_main_loop(update_draw_frame, 0, 1)
## else
   while not Raylib.WindowShouldClose() do
      update_draw_frame()
   end
##end

-- ] Game loop ]

-- [ Game De-initialization [
Raylib.CloseWindow()
-- ] Game De-initialization ]
