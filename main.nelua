require 'raylib'
require 'rotor'

require 'config'

require 'string'
require 'vector'

-- [ Main game records  and enums[
global EntityTag = @enum {
   Player = 1,
   TileMap = 2,
   Wall = 4,
   CogSlot = 8,
   PuzzleWall = 16,
   Box = 32,
   Cog = 64,
}

global CollisionState = @enum {
   None = 0,
   Enter,
   Stay
}

require 'game'
-- ] Main game records ]

-- [ Game initialization [
global Globals: record{
   player_input: record{
      direction: Vector2,
      action: boolean
   },
   camera: record{
      position: Vector2,
      camera: Camera2D
   },
   game_window: record{
      width: integer,
      height: integer,
      title: string
   },
   game_systems: record{
      colliders_sync: CollidersSync,
      sprite_painter: SpritePainter,
      animation: Animation,
   },
   untracked_ids: vector(GenerationalIndex),
} = {
   player_input = {
      direction = { 0, 0 },
      action = false,
   },
   camera = {
      position = { #[PL_POS_X]#, #[PL_POS_Y]# },
      camera = {
         offset = { #[WIN_WIDTH]# / 2, #[WIN_HEIGHT]# / 2 },
         target = { #[PL_POS_X]#, #[PL_POS_Y]# },
         rotation = 0,
         zoom = 1
      }
   },
   game_window = {
      #[WIN_WIDTH]# // 1,
      #[WIN_HEIGHT]# // 1,
      #[WIN_TITLE]#
   },
   untracked_ids = {},
   game_systems = {
      colliders_sync = CollidersSync.new(),
      sprite_painter = SpritePainter.new(),
      animation = Animation.new()
   },
}

Raylib.InitWindow(Globals.game_window.width, Globals.game_window.height, Globals.game_window.title)
Raylib.SetTargetFPS(60)

local function update_all_systems()
   Globals.game_systems.colliders_sync.system.system_data:update(storages.entity, Globals.untracked_ids)
   Globals.game_systems.sprite_painter.system.system_data:update(storages.entity, Globals.untracked_ids)
   Globals.game_systems.animation.system.system_data:update(storages.entity, Globals.untracked_ids)
end

local function create_entity_test(pos: Vector2, fi: uinteger, cw: uinteger): TestEntity_1
   local test_collider: Collider = {
      local_rect = { 0, 16, cw, 16},
      global_rect = { 0, 16, cw, 16},
      entity_id = { -1, 0 },
      entity_tag = EntityTag.Player
   }

   local test_sprite: Sprite = {
      sprite_sheet = Raylib.LoadTexture("resources/prototype-tileset.png"),
      frame_index = fi,
      frame_rect = { 0, 0, 32, 32 }
   }

   local test_animations: Animations = {
      animations = {
         {frame_indexes = {0, 1, 2}, frame_duration = 0.5},
         {frame_indexes = {4, 9, 14, 19}, frame_duration = 0.1},
      },
      current_animation_index = 0,
      current_frame_index = 0,
      elapsed_time = 0,
      paused = false
   }

   local new_test: TestEntity_1 = {}

   new_test:create(
      storages.entity,
      storages.position, { position = pos },
      storages.velocity, { direction = { 0, 0 }, speed = 1 },
      storages.collider, test_collider,
      storages.collisions,
      storages.sprite, test_sprite,
      storages.animations, test_animations,
      Globals.untracked_ids
   )

   return new_test
end

local test_entity_1 = create_entity_test({20, 20}, 0, 32)
local test_entity_2 = create_entity_test({30, 15}, 1, 16)

update_all_systems()

local test_position_opt = storages.position:get_entry(test_entity_2.position_id)
local test_position: Position* = test_position_opt:get()

local test_animations_opt = storages.animations:get_entry(test_entity_2.animations_id)
local test_animations: Animations* = test_animations_opt:get()
test_animations.current_animation_index = 1

-- ] Game initialization ]

-- [ Game loop [
while not Raylib.WindowShouldClose() do
   -- [ Update [
   local dt = Raylib.GetFrameTime()

   test_position.position.y = Raylib.GetMouseY()

   Globals.game_systems.animation:run(storages.animations, storages.sprite, dt)
   -- ] Update ]

   -- [ Draw [
   Raylib.BeginDrawing()
      Raylib.ClearBackground(RAYWHITE)

      Globals.game_systems.sprite_painter:run(storages.sprite, storages.position)
   Raylib.EndDrawing()
   -- ] Draw ]
end
-- ] Game loop ]

-- [ Game De-initialization [
Raylib.CloseWindow()
-- ] Game De-initialization ]
